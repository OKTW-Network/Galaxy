buildscript {
    ext.kotlin_version = '1.2.21'
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "java"
    id "idea"
    id "signing"
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id 'net.minecrell.vanillagradle.server' version '2.2-6'
}
apply plugin: 'kotlin'

group "one.oktw"
version "1.0-SNAPSHOT"

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

ext.spongeVersion = "7.0.0"
ext.CoroutinesVersion = "0.21.2"


minecraft {
    version = "1.12.2"
    mappings = "snapshot_20180207"
}

dependencies {
    compile "org.spongepowered:spongeapi:$spongeVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$CoroutinesVersion"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$CoroutinesVersion"
    compile "org.mongodb:mongodb-driver:3.6.3"
//    compile "com.github.NucleusPowered.Nucleus:nucleus-api:1.2.2"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = null
    exclude "META-INF/*"
    dependencies {
        include dependency("org.mongodb:")
        include dependency("org.jetbrains.kotlin:")
        include dependency("org.jetbrains.kotlinx:")
    }

    relocate "com.mongodb", "one.oktw.relocate.com.mongodb"
    relocate "org.bson", "one.oktw.relocate.org.bson"
    relocate "kotlin", "one.oktw.relocate.kotlin"
    relocate "kotlinx", "one.oktw.relocate.kotlinx"
}

signing {
    required {signing.hasProperty('keyId')}
    sign configurations.archives
}

jar.dependsOn(shadowJar)
jar.enabled = false