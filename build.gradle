buildscript {
    ext.kotlin_version = '1.3.21'
    ext.sponge_version = "7.1.0"
    ext.Coroutines_version = "1.0.1"

    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

plugins {
    id "java"
    id "idea"
    id "signing"
    id "org.spongepowered.plugin" version "0.9.0"
    id "com.github.johnrengelman.shadow" version "4.0.3"
}
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

group "one.oktw"
version "1.0-SNAPSHOT"

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    mappings = "stable_39"
    makeObfSourceJar = false
}

dependencies {
    compile "org.spongepowered:spongeapi:$sponge_version"
    compile 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$Coroutines_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$Coroutines_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:$Coroutines_version"
    compile "org.mongodb:mongodb-driver-reactivestreams:1.10.0"
    compile "com.github.OKTW-Network:sponge-i18n:0.1.5:api"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        ext.refMap = "mixins.galaxy.refmap.json"
    }
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

extractAnnotationsJar.enabled = false

jar {
    manifest.attributes(
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'MixinConfigs': 'mixins.galaxy.json',
        'FMLCorePluginContainsFMLMod': true,
        'ForceLoadAsMod' : true
    )
}

shadowJar {
    classifier = null
//    exclude "META-INF/*"
    exclude "dummyThing"
    dependencies {
        include dependency("org.mongodb:")
        include dependency("org.reactivestreams:")
        include dependency("org.jetbrains.kotlin:")
        include dependency("org.jetbrains.kotlinx:")
    }

    relocate "com.mongodb", "one.oktw.relocate.com.mongodb"
    relocate "org.bson", "one.oktw.relocate.org.bson"
    relocate "org.reactivestreams", "one.oktw.relocate.org.reactivestreams"
    relocate "kotlin", "one.oktw.relocate.kotlin"
    relocate "kotlinx", "one.oktw.relocate.kotlinx"
    relocate "com.typesafe", "configurate.typesafe"
}

signing {
    required {signing.hasProperty('keyId')}
    sign configurations.archives
}

jar.dependsOn(shadowJar)
jar.enabled = false
