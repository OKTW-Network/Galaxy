buildscript {
    ext.kotlin_version = '1.2.51'
    ext.sponge_version = "7.1.0-SNAPSHOT"
    ext.Coroutines_version = "0.23.4"

    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "java"
    id "idea"
    id "signing"
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'

group "one.oktw"
version "1.0-SNAPSHOT"

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

minecraft {
    version = "1.12.2-14.23.4.2705"
    mappings = "snapshot_20180708"
    makeObfSourceJar = false
}

dependencies {
    compile "org.spongepowered:spongeapi:$sponge_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$Coroutines_version"
    compile "org.mongodb:mongodb-driver-sync:3.8.0"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = null
    exclude "META-INF/*"
    exclude "dummyThing"
    dependencies {
        include dependency("org.mongodb:")
        include dependency("org.jetbrains.kotlin:")
        include dependency("org.jetbrains.kotlinx:")
    }

    relocate "com.mongodb", "one.oktw.relocate.com.mongodb"
    relocate "org.bson", "one.oktw.relocate.org.bson"
    relocate "kotlin", "one.oktw.relocate.kotlin"
    relocate "kotlinx", "one.oktw.relocate.kotlinx"
}

signing {
    required {signing.hasProperty('keyId')}
    sign configurations.archives
}

jar.dependsOn(shadowJar)
jar.enabled = false
